// Ascension
// https://weidu.org/asc.html ( outdated, use https://forums.beamdog.com/discussion/66702/ascension-for-bg2-ee )

BACKUP ~ascension/backup~
// AUTHORS: David Gaider & Contributers, Westley Weimer, Turumbar, Miloch, Cuv 
// EE adoption: WhiteAgnus, JediMindTrix at http://s13.zifboards.com/Project_Ascension/index.php?showforum=2"
SUPPORT "https://forums.beamdog.com/discussion/66702/ascension-for-bg2-ee"

// use Semantic Versioning - http://semver.org
VERSION ~1.7.2~

AUTO_TRA ~ascension/tra/autotra/%s~

ALWAYS
	INCLUDE ~ascension/lib/always.tpa~ //library with functions and macros common to all components
	INCLUDE ~ascension/lib/charset.tpa~
	INCLUDE ~ascension/lib/ds.tpa~
END

// Language Options
//
// If you list more than one language, the user gets a choice.
// The first argument is the name of the language as presented to the user,
// the second is the translation directory name, and any others arguments
// are translation files that should be loaded immediately,
// because they contain the translated names of the modules the user will install.

LANGUAGE ~English~    
         ~english~ 
         ~ascension/tra/english/setup.tra~

LANGUAGE ~Deutsche Sprache (sbersetzung durch Dyara & Falk Swoboda)~
         ~german~ 
         ~ascension/tra/german/setup.tra~

LANGUAGE ~Francais (traduction de Ly Meng)~
         ~french~ 
         ~ascension/tra/french/setup.tra~

LANGUAGE ~Traducción al Español (hecha por Clan REO)~
         ~spanish~
         ~ascension/tra/spanish/setup.tra~

LANGUAGE ~Chinese Translation (by kalabaka)~ 
         ~schinese~
         ~ascension/tra/schinese/setup.tra~

LANGUAGE ~Traditional Chinese Translation (by kalabaka)~ 
         ~tchinese~
         ~ascension/tra/tchinese/setup.tra~

LANGUAGE ~Polski jezyk (Przetlumaczony przez Damian Staniszewski)~ 
         ~polish~
         ~ascension/tra/polish/setup.tra~

LANGUAGE ~Russian (Pilferer, Serdrick, Aerie.ru)~
         ~russian~
         ~ascension/tra/russian/setup.tra~

LANGUAGE ~Cesky jazyk (Landor, Pan Baca)~
         ~czech~
         ~ascension/tra/czech/setup.tra~

LANGUAGE ~Italian (Al17, Alessandro, Max, M.Inxes, Rahar and Stefano)~
         ~italian~
         ~ascension/tra/italian/setup.tra~

///////////////////////////////////////////////////////
// The Ascension (WhiteAgnus / JediMindTrix version) //
///////////////////////////////////////////////////////

BEGIN @1000 DESIGNATED 0
	REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1001 // ToB is required
	REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ OR MOD_IS_INSTALLED ~setup-bg2fixpack.tp2~ ~0~ @1003

	INCLUDE ~ascension/lib/epilogue_update.tph~
	INCLUDE ~ascension/lib/ascension.tpa~

	COPY_EXISTING ~sw1h01.itm~ ~override/asc_main.flag~ //Creates install "flag"

////////////////////////////////////////////////
// Restored Bhaalspawn Powers by David Gaider //	
////////////////////////////////////////////////

BEGIN @4000 DESIGNATED 103
	REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1001 // ToB is required
	COPY_EXISTING ~sw1h01.itm~ ~override/asc_rest_powers.flag~ //Creates install "flag"

	INCLUDE ~ascension/lib/bhaalspawnpower.tpa~
	
	ADD_JOURNAL @2000 @2001 @2002 @2003 @2004 @2005 @2006 @2007

	
///////////////////////////////////////////////////////////////////////
// Tougher Abazigal
///////////////////////////////////////////////////////////////////////

BEGIN @2100 DESIGNATED 1
	REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1001 // ToB is required
	REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ OR MOD_IS_INSTALLED ~setup-bg2fixpack.tp2~ ~0~ @1003
	
	INCLUDE ~ascension/lib/tougherabazigal.tpa~

	COPY_EXISTING ~sw1h01.itm~ ~override/asc_abazigal.flag~ //Creates install "flag"

////////////////////////
// Tougher Demogorgon //
////////////////////////

BEGIN @2200 DESIGNATED 2
	REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1001 // ToB is required
	REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ OR MOD_IS_INSTALLED ~setup-bg2fixpack.tp2~ ~0~ @1003

	INCLUDE ~ascension/lib/tougherdemo2.tpa~

	COPY_EXISTING ~sw1h01.itm~ ~override/asc_demogorgon.flag~ //Creates install "flag"

/////////////////////
// Tougher Gromnir //
/////////////////////

BEGIN @2300 DESIGNATED 3
	REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1001 // ToB is required
	REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ OR MOD_IS_INSTALLED ~setup-bg2fixpack.tp2~ ~0~ @1003

	INCLUDE ~ascension/lib/toughergrom.tpa~

	//Compile common scripts, if Tougher Yaga-Shura hasn't been installed already
	ACTION_IF !FILE_EXISTS ~override/asc_yagashura.flag~ BEGIN
		INCLUDE ~ascension/lib/toughercommon.tpa~
	END

	COPY_EXISTING ~sw1h01.itm~ ~override/asc_gromnir.flag~ //Creates install "flag"

//////////////////////
// Tougher Illasera //
//////////////////////

BEGIN @2400 DESIGNATED 4
	REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1001 // ToB is required
	REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ OR MOD_IS_INSTALLED ~setup-bg2fixpack.tp2~ ~0~ @1003

	INCLUDE ~ascension/lib/tougherillasera.tpa~

	COPY_EXISTING ~sw1h01.itm~ ~override/asc_illasera.flag~ //Creates install "flag"

////////////////////////
// Tougher Yaga-Shura //
////////////////////////

BEGIN @2500 DESIGNATED 5
	REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1001 // ToB is required
	REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ OR MOD_IS_INSTALLED ~setup-bg2fixpack.tp2~ ~0~ @1003

	INCLUDE ~ascension/lib/tougheryaga.tpa~

	//Compile common scripts, if Tougher Gromnir hasn't been installed already
	ACTION_IF !FILE_EXISTS ~override/asc_gromnir.flag~ BEGIN
		INCLUDE ~ascension/lib/toughercommon.tpa~
	END

	COPY_EXISTING ~sw1h01.itm~ ~override/asc_yagashura.flag~ //Creates install "flag"

///////////////////////////////////////////////////
// Extended Epilogues for Bioware NPCs by shawne //
///////////////////////////////////////////////////

BEGIN @5100 DESIGNATED 104
	REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1001 // ToB is required
	COPY_EXISTING ~sw1h01.itm~ ~override/asc_shawnes.flag~ //Creates install "flag"

	INCLUDE ~ascension/lib/epilogue_update.tph~

	LOAD_TRA ~ascension/tra/autotra/%LANGUAGE%/shawnesepilogues.tra~

	LAF update_epilogues STR_VAR filepath = ~ascension/lib/shawnesepilogues.tbl~ END

	ACTION_IF FILE_EXISTS ~override/asc_main.flag~ BEGIN // additional epilogues require Ascension
	  LAF update_epilogues STR_VAR filepath = ~ascension/lib/shawnesepilogues_asconly.tbl~ END
	END

///////////////////////////////////////////////////
// Extended Epilogues for Beamdog NPCs by shawne //
///////////////////////////////////////////////////

BEGIN @5200 DESIGNATED 105
	REQUIRE_PREDICATE GAME_IS ~bg2ee eet~ @5220
	COPY_EXISTING ~sw1h01.itm~ ~override/asc_shawnesEE.flag~ //Creates install "flag"

	INCLUDE ~ascension/lib/epilogue_update.tph~

	EXTEND_TOP ~ar6200.bcs~ ~ascension/shawnesEE/baf/eAR6200.baf~

	COPY ~ascension/shawnesEE/2da~ ~override~

	LOAD_TRA ~ascension/tra/autotra/%LANGUAGE%/shawnesepilogues_eenpc.tra~

	LAF update_epilogues STR_VAR filepath = ~ascension/lib/shawnesepiloguesEE.tbl~ END

////////////////////////////////
// Ascension - Epilogues Only //
////////////////////////////////

BEGIN @5300 DESIGNATED 106
	REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1001 // ToB is required
	FORBID_FILE ~override/asc_main.flag~ @5310

	INCLUDE ~ascension/lib/epilogue_update.tph~

	LAF update_epilogues STR_VAR filepath = ~ascension/lib/epilogues.tbl~ END

	COPY_EXISTING ~sw1h01.itm~ ~override/asc_epi_only.flag~ //Creates install "flag"

/////////////////////////////////
// Ascension Portraits - Bodhi //	
/////////////////////////////////

BEGIN @3010 DESIGNATED 101
	REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1001 // ToB is required
	REQUIRE_FILE ~override/asc_main.flag~ @1002

	INCLUDE ~ascension/lib/cd_portrait_copy.tph~
	LAF cd_portrait_npc STR_VAR source = abodhi source_path = ~ascension/ascports/altbodhi~ END

	COPY_EXISTING ~FINBODH.CRE~ ~override~
		WRITE_ASCII 0x34 ~ABODHIM~ #8 //Portrait Swap
		WRITE_ASCII 0x3c ~ABODHIL~ #8 //Portrait Swap
	BUT_ONLY

	COPY_EXISTING ~sw1h01.itm~ ~override/asc_altbodhi.flag~ //Creates install "flag"

/////////////////////////////////////////////////////////////////
// Ascension Portraits - Balthazar by Cuv ( Alternate Design ) //	
/////////////////////////////////////////////////////////////////

BEGIN @3020 DESIGNATED 102
	REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1001 // ToB is required
	REQUIRE_FILE ~override/asc_main.flag~ @1002

	INCLUDE ~ascension/lib/cd_portrait_copy.tph~
	LAF cd_portrait_npc STR_VAR source = CBALTHA source_path = ~ascension/ascports/cuvbalth~ END

	COPY_EXISTING ~balth.cre~ ~override~   ~balth2.cre~ ~override~   ~finbalth.cre~ ~override~
		WRITE_ASCII 0x34 ~CBALTHAM~ #8 //Portrait Swap
		WRITE_ASCII 0x3c ~CBALTHAL~ #8 //Portrait Swap
	BUT_ONLY

	COPY_EXISTING ~sw1h01.itm~ ~override/asc_altbalth.flag~ //Creates install "flag"
